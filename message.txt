(*
    The interval domain
*)

open Abstract_syntax_tree

module Intervals  = struct
  include Value_domain

  type bound = Int of Z.t | PINF | MINF

  type t = Itv of bound * bound | BOT

  (* print *)
  let bound_to_string (t1 : bound) =
    match t1 with Int v -> Z.to_string v | PINF -> "+∞" | MINF -> "-∞"

  (* negation *)
  let bound_neg (a : bound) : bound =
    match a with MINF -> PINF | PINF -> MINF | Int i -> Int (Z.neg i)

  (* a+b *)
  let bound_add (a:bound) (b:bound) : bound = match a,b with
    | MINF,PINF | PINF,MINF -> invalid_arg "bound_add" (* (+∞) + (−∞) *)
    | MINF,_ | _,MINF -> MINF
    | PINF,_ | _,PINF -> PINF
    | Int i, Int j -> Int (Z.add i j)
    

  (* a * b *)
  let  bound_mult (a: bound) (b: bound) : bound = match a,b with
    |Int c, PINF | PINF, Int c -> if (Z.equal c Z.zero)
      then Int Z.zero 
      else (if(Z.gt c Z.zero)
            then  PINF
            else  MINF)
    |MINF, Int c | Int c, MINF -> if (Z.equal c Z.zero)
      then Int Z.zero 
      else (if (Z.gt c Z.zero) 
            then  MINF
            else  PINF)
    |Int c1, Int c2 -> if (Z.equal c1 Z.zero)
      then Int Z.zero
      else (if (Z.equal c2 Z.zero)
            then Int Z.zero
            else Int (Z.mul c1 c2)
           )


    | MINF,PINF | PINF,MINF -> MINF

    | PINF,PINF  | MINF,MINF  -> PINF

  (* a / b *)
  let bound_div (a: bound) (b: bound) : bound = match a,b with 
    | PINF,PINF -> Int (Z.zero)
    | _,MINF | _,PINF -> Int (Z.zero)
    | PINF, Int c -> if (Z.gt c Z.zero)
      then PINF
      else ( 
        if (Z.lt c Z.zero)
        then MINF
        else invalid_arg "div by 0"
      )
    | MINF, Int c ->  if (Z.gt c Z.zero)
      then MINF
      else ( 
        if (Z.lt c Z.zero)
        then PINF
        else invalid_arg "div by 0"
      )

    | Int c1, Int c2 -> Int (Z.div c1 c2)



  (* compare a et b, retourne -1, 0 ou 1 *)
  let bound_cmp (a : bound) (b : bound) : int =
    match (a, b) with
    | MINF, MINF | PINF, PINF -> 0
    | MINF, _ | _, PINF -> -1
    | PINF, _ | _, MINF -> 1
    | Int i, Int j -> Z.compare i j

  (* minimum bound *)
  let bound_min (a : bound) (b : bound) : bound =
    match bound_cmp a b with
    | -1 -> a
    | _ -> b


  (* maximum bound *)
  let bound_max (a : bound) (b : bound) : bound =
    match bound_cmp a b with
    | -1 -> b
    | _ -> a 


  (* extension de f par f(⊥) = ⊥ *)
  let lift1 f x = match x with Itv (a, b) -> f a b | BOT -> BOT

  let lift2 f x y = match x,y with
    |BOT,_|_,BOT -> BOT
    |Itv(a1,b1),Itv(a2,b2) -> f a1 b1 a2 b2

  (* −x dans les intervalles *)
  let neg (x : t) : t = lift1 (fun a b -> Itv (bound_neg b, bound_neg a)) x

  let top = Itv (MINF, PINF)

  let bottom = BOT

  (* [a1,b1] + [a2,b2] *)
  let add (a:t) (b:t) : t = lift2 (fun a1 b1 a2 b2 -> Itv(bound_add a1 a2, bound_add b1 b2)) a b 

  (* [a1,b1] - [a2,b2] *)
  let sub (a:t) (b:t) : t = lift2 (fun a1 b1 a2 b2 -> Itv(bound_add a1 (bound_neg b2), bound_add b1 (bound_neg a2))) a b 

  (* [a1,b1] * [a2,b2] *)
  let mult (a:t) (b:t) : t = lift2 (fun a1 b1 a2 b2 -> Itv(bound_min (bound_min (bound_mult a1 a2) (bound_mult a1 b2)) (bound_min  (bound_mult b1 a2) (bound_mult b1 b2) ) 
                                                          , bound_max (bound_max (bound_mult a1 a2) (bound_mult a1 b2))  (bound_max (bound_mult b1 a2) (bound_mult b1 b2)))) a b

  (* [a1,b1] / [a2,b2] *)
  let div  (a:t) (b:t) : t = lift2 (fun a1 b1 a2 b2 ->  match (bound_cmp (Int Z.one) a2) with
      | -1 | 0 -> Itv(bound_min (bound_div a1 a2) (bound_div a1 b2), bound_max (bound_div  b1 a2) (bound_div b1 b2) )
      | _ -> (match (bound_cmp b2 (Int (Z.neg Z.one))) with
          | -1 | 0 -> Itv(bound_min (bound_div b1 a2) (bound_div b1 b2), bound_max (bound_div  a1 a2) (bound_div a1 b2) )  
          | _ -> (match a2, b2 with
              | Int c1, Int c2 -> if((Z.equal Z.zero c1) && (Z.equal Z.zero c2))
                then BOT
                else Itv(bound_div a1 a2, bound_div b1 b2)
              | _ ->  Itv(bound_div a1 a2, bound_div b1 b2)
            )
        )
    ) a b

  (* constant: {c} *)
  let const c = Itv (Int c, Int c)

  (* interval: [a,b] *)
  let rand a b =
    match Z.leq a b with true -> Itv (Int a, Int b) | false -> BOT

  let join a b = match a,b with
    | BOT,c | c,BOT -> c
    | Itv(a1,b1),Itv(a2,b2) -> Itv(bound_min a1 a2, bound_max b1 b2)


  let meet a b =  match a,b with
    | BOT,_ | _,BOT -> BOT 
    | Itv(a1,b1), Itv(a2,b2) ->( match bound_cmp (bound_max a1 a2) (bound_min b1  b2) with
        | -1 | 0 -> Itv((bound_max a1 a2),(bound_min b1  b2)) 
        | _ -> BOT
      ) 

  (* x ⊆ y dans les intervalles *)
  let subset (x : t) (y : t) : bool =
    match (x, y) with
    | BOT, _ -> true
    | _, BOT -> false
    | Itv (a, b), Itv (c, d) -> bound_cmp a c >= 0 && bound_cmp b d <= 0

  let is_bottom a = (a=BOT)

  (* print abstract element *)
  let print fmt x =
    match x with
    | BOT -> Format.fprintf fmt "⊥"
    | Itv (t1, t2) ->
      Format.fprintf fmt "[%s;%s]" (bound_to_string t1) (bound_to_string t2)

  (*let print2  x =
    match x with
    | BOT ->  "⊥"
    | Itv (t1, t2) ->
      "["^(bound_to_string t1)^","^(bound_to_string t2)^"]"*)

  let unary a op =
    match op with AST_UNARY_PLUS -> a | AST_UNARY_MINUS -> neg a

  let binary a b op =
    match op with
    | AST_PLUS -> add a b
    | AST_MINUS -> sub a b
    | AST_MULTIPLY -> mult a b
    | AST_DIVIDE -> div a b

  let widen a b = match a,b with
    | x,BOT -> x
    | BOT,x -> x
    | Itv(a1,b1),Itv(a2,b2) -> (match bound_cmp a1 a2 with
        | -1 | 0 -> (match bound_cmp b1 b2 with
            | 1 | 0 -> Itv(a1,b1)
            | _ -> Itv(a1,PINF)
          )
        | _ -> (match bound_cmp b1 b2 with
            | 1 | 0 -> Itv(MINF,b1)
            | _ -> Itv(MINF,PINF)
          )
      )

  (* implémentation des opérations de comparaison *)

  (* égalités *)
  let eq a b = meet a b, meet a b 

  (* la  non-égalité *)
  let neq a b = 
    match a,b with
    | BOT,_ -> BOT,b
    | _,BOT -> a,BOT
    | Itv(a1,b1),Itv(a2,b2) -> ( match ((bound_cmp a1 a2),(bound_cmp b1 a2),(bound_cmp a1 b2) ,(bound_cmp b1 b2)) with
        | _,-1,_,_ -> a,b
        | _,_,1,_  -> a,b 
        | 0,_,_,0 ->  a,b
        | -1,0,_,_ -> Itv(a1,bound_add a2 (bound_neg (Int Z.one))), b
        | -1,1,_,_ -> Itv(a1,bound_add a2 (bound_neg (Int Z.one))), b
        | _,_,_,0 -> BOT,BOT
        | _,_,_,-1 -> BOT,BOT
        | _,_,_,_ -> Itv(bound_add b2 (Int Z.one),b1),b
      )


  let gt x y  = match x, y with
                |BOT, _     -> BOT, BOT
                |Itv(a,b), Itv(c,d) when (bound_cmp a c > 0) &&  (bound_cmp b d > 0) -> x,y
                |Itv(a, b), Itv(c, d)  -> if bound_cmp b c <= 0 then BOT, BOT
                              else if (bound_cmp a c == 1) &&  (bound_cmp b d  == 1) then  x,y 
                                               else   let bound_max  =  bound_max a (bound_add  c (Int Z.one))  in
                                                      let bound_min  =  bound_min (bound_add b (Int (Z.neg (Z.one))) ) d  in 
                                                       Itv (bound_max, b), Itv (c, bound_min)
                | _, _   -> x, y

  let geq x y  = match x, y with
                  |BOT, _                -> BOT, BOT
                  |Itv(a, b), Itv(c, d)  -> if bound_cmp b c < 0 then BOT, BOT
                                            else   let bound_max  = bound_max a c in
                                                   let bound_min  = bound_min b d  in 
                                                     Itv (bound_max, b), Itv (c, bound_min)
                  | _, _   -> x, y


  let compare a b op = 
    match op with
    | AST_EQUAL -> eq a b
    | AST_NOT_EQUAL -> neq a b
    | AST_GREATER_EQUAL -> geq a b
    | AST_GREATER -> gt a b
    | AST_LESS_EQUAL ->  
      let y', x' = geq b a in
      (x', y')
    | AST_LESS -> 
      let y', x' = gt b a in
      (x', y')

  (* a vérifier auprés du prof  *)
  let bwd_unary x op r = match op with
    | AST_UNARY_PLUS -> meet x r
    | AST_UNARY_MINUS -> meet x (neg r)

  (* a vérifier auprés du prof/ meme que les constantes  *)
  let bwd_binary x y op r =
    match op with
    | AST_PLUS ->
        (* r=x+y => x=r-y and y=r-x *)
        (meet x (sub r y), meet y (sub r x))
    | AST_MINUS ->
        (* r=x-y => x=y+r and y=x-r *)
        (meet x (add y r), meet y (sub x r))
    | AST_MULTIPLY ->
        (* r=x*y => (x=r/y or y=r=0) and (y=r/x or x=r=0)  *)
        let contains_zero o = subset (const Z.zero) o in
        ( (if contains_zero y && contains_zero r then x else meet x (div r y)),
          if contains_zero x && contains_zero r then y else meet y (div r x) )
    | AST_DIVIDE ->
        (* this is sound, but not precise *)
        meet x (mult y r), meet y (div x r)
end
